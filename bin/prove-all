#!/usr/bin/env perl

use warnings;
use strict;
use autodie;
use Carp;
use FindBin;
use Mojo::File;
use feature 'say';
use Git::Repository;
use Term::ANSIColor;

=head1 NAME

prove-all

=head1 DESCRIPTION

Check if git is in sync and test all repos

=cut

sub prove {
		chdir shift;
		my $success = 'Result: PASS';
		my $prove   = `prove -l -Q`;

		if ($prove =~ /$success/) {
		    say $success;
		} else {
		    say $prove;
		}
 }

my $gitdir = "$FindBin::Bin/../..";
opendir (my $dh , $gitdir);
while (my $item = readdir $dh) {
    next if $item =~/^\./;
    my $wd = "$gitdir/$item";
    next if ! -d $wd;
    next if ! -d "$wd/.git";
    next if ! -d "$wd/lib" && ! -d "$wd/t";
	print color('blue');
    say "$wd";
	print color('reset');
    my $r = Git::Repository->new( work_tree => $wd );
    $r->run( 'remote','update');
    my $output = $r->run( 'status' );
    if ($output =~/On branch \w+\s*\nYour branch is up[-\s]to[-\s]date.+nothing to commit\, working tree clean/ms) {
        $r->run( 'pull' );
        prove($wd);
        next;
    }
    elsif ( $output =~ /Your branch is behind '[\w\/]+' by \d+ commits, and can be fast-forwarded./ms ) {
        $r->run( 'pull' );
        prove($wd);
        next;
    }
    say $output;
#    die "git repo is unclean.";
}
closedir $dh;
